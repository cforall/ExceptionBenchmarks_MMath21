// Resume Across Empty Function
#include <clock.hfa>
#include <exception.hfa>
#include <fstream.hfa>
#include <stdlib.hfa>

exception fixup_exception {
	int & fixup;
};
vtable(fixup_exception) fixup_vt;

void nounwind_empty(unsigned int frames) {
	if (frames) {
		nounwind_empty(frames - 1);
		// "Always" false, but prevents recursion elimination.
		if (-1 == frames) printf("~");
	} else {
		int fixup = 17;
		throwResume (fixup_exception){&fixup_vt, fixup};
	}
}

int main(int argc, char * argv[]) {
	unsigned int times = 1;
	unsigned int total_frames = 1;
	if (1 < argc) {
		times = strto(argv[1], 0p, 10);
	}
	if (2 < argc) {
		total_frames = strto(argv[2], 0p, 10);
	}

	Time start_time = timeHiRes();
	for (unsigned int count = 0 ; count < times ; ++count) {
		try {
			nounwind_empty(total_frames);
		} catchResume (fixup_exception * ex) {
			ex->fixup = total_frames + 42;
		}
	}
	Time end_time = timeHiRes();
	sout | "Run-Time (s): " | wd(0,1, (end_time - start_time)`ns / 1_000_000_000.);
}
